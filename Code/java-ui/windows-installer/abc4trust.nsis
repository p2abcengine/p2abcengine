!include WinMessages.nsh
!include LogicLib.nsh
!include FileFunc.nsh
!include "MUI.nsh"

#
# License settings
#

# Should we display the Idemix license during installation? Valid values are "Yes" and "No"
!define DISPLAY_IDEMIX_LICENSE "Yes"

#
# Other file locations. You probably shouldn't change these unless you have a good reason to do so
#

# URL where Java JRE can be download. Set to the empty string if JRE should not be downloaded automatically
!define JRE_URL ""

# Location of the Idemix license
!define IDEMIX_LICENSE "license\Identity Mixer License 1.2.rtf"

#
# Basic information about the application to be installed. Change this if you like
#

!define COMPANYNAME "ABC4Trust"
!define APPNAME "User Client"
# These three must be integers
!define VERSIONMAJOR 1
!define VERSIONMINOR 0
!define VERSIONBUILD 0

# Images for the installer
!define ABC4TRUST_ICON "graphics\abc4trust.ico"
!define ABC4TRUST_HEADER_IMAGE "graphics\abc4trust_header.bmp"
!define ABC4TRUST_WELCOME_IMAGE "graphics\abc4trust_welcome.bmp"

# Default install location
InstallDir "$PROGRAMFILES\${COMPANYNAME}\${APPNAME}"
InstallDirRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "InstallLocation"

# ID and installation directory (as subdirectory of $INSTALLDIR) for browser extensions
!define FIREFOX_EXTENSION_ID "abc4trust@abc4trust.eu"
!define FIREFOX_EXTENSION_DIR "Extensions\abc4trust-firefox"
!define IE_EXTENSION_DIR "Extensions\abc4trust-ie"
!define RESOURCES_DIR "resources"

# Installer icon
!define MUI_ICON "${ABC4TRUST_ICON}"
!define MUI_UNICON "${ABC4TRUST_ICON}"

# Header image
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "${ABC4TRUST_HEADER_IMAGE}"
!define MUI_HEADERIMAGE_UNBITMAP "${ABC4TRUST_HEADER_IMAGE}"
!define MUI_HEADERIMAGE_NOSTRETCH
!define MUI_WELCOMEFINISHPAGE_BITMAP "${ABC4TRUST_WELCOME_IMAGE}"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "${ABC4TRUST_WELCOME_IMAGE}"
!define MUI_ABORTWARNING

# Pages
# Uncomment the line below to keep log window open after install
#!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE RunUninstallerIfNeeded
!insertmacro MUI_PAGE_WELCOME
!define MUI_PAGE_CUSTOMFUNCTION_PRE SkipPageIfBeingUpgraded
!insertmacro MUI_PAGE_LICENSE "${IDEMIX_LICENSE}"
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!define MUI_PAGE_CUSTOMFUNCTION_PRE un.SkipPageIfBeingUpgraded
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_INSTFILES
!define MUI_PAGE_CUSTOMFUNCTION_PRE un.SkipPageIfBeingUpgraded
!insertmacro MUI_UNPAGE_FINISH

#
# Language settings
#

# Language
!insertmacro MUI_LANGUAGE "English"

# Translate these as needed
LangString ADMIN_REQUIRED ${LANG_ENGLISH} "Administrator rights required"

LangString START_APPLICATION ${LANG_ENGLISH}  "Starting ABCE"

LangString STOP_APPLICATION ${LANG_ENGLISH} "Stopping ABCE"

LangString EXTRACTING_FILES ${LANG_ENGLISH} "Extracting files"

LangString JRE_REQUIRED_CAN_DOWNLOAD ${LANG_ENGLISH} "${COMPANYNAME} ${APPNAME} requires Java, it will now be downloaded and installed."

LangString JRE_REQUIRED_CANNOT_DOWNLOAD ${LANG_ENGLISH} "${COMPANYNAME} ${APPNAME} requires Java. Please install JRE and restart this installer."

LangString JRE_DOWNLOAD_FAILED ${LANG_ENGLISH} "Java download failed. Please install JRE manually and restart this installer."

LangString UNINSTALL_CONFIRMATION ${LANG_ENGLISH} "${COMPANYNAME} ${APPNAME} is already installed. $\n$\nClick OK to upgrade the old version or Cancel to abort."

LangString CLOSE_IE ${LANG_ENGLISH} "Close Internet Explorer and click OK to continue or Cancel to abort."

LangString REGISTERING_BROWSER_PLUGINS ${LANG_ENGLISH} "Registering browser plugins."

LangString UNREGISTERING_BROWSER_PLUGINS ${LANG_ENGLISH} "Unregistering browser plugins."

LangString COULD_NOT_EXTRACT ${LANG_ENGLISH} "Could not extract files. Close the application if it is running and try again."

#
# You shouldn't normally need to change anything below this line
#

# This will be in the installer/uninstaller's title bar
Name "${COMPANYNAME} ${APPNAME}"
# Output file for the installer
outFile "install.exe"

# We require administrative access for installation
RequestExecutionLevel admin

# Global parameters
Var /GLOBAL DISPLAYING_IDEMIX_LICENSE

Function DetectJRE
  ReadRegStr $2 HKLM "SOFTWARE\JavaSoft\Java Runtime Environment" "CurrentVersion"
  StrCmp $2 "1.7" done
  StrCmp $2 "1.8" done
 
  Call GetJRE
 
  done:
FunctionEnd

Function GetJRE
        StrCmp "${JRE_URL}" "" +3
		    MessageBox MB_OK "$(JRE_REQUIRED_CAN_DOWNLOAD)"
			Goto CanDownloadJRE
		MessageBox MB_OK "$(JRE_REQUIRED_CANNOT_DOWNLOAD)"
		Quit
CanDownloadJRE:			
        StrCpy $2 "$TEMP\Java Runtime Environment.exe"
        nsisdl::download /TIMEOUT=30000 "${JRE_URL}" $2
        Pop $R0 ;Get the return value
        StrCmp $R0 "success" +3
                MessageBox MB_OK "$(JRE_DOWNLOAD_FAILED): $R0"
                Quit
        ExecWait $2
        Delete $2
FunctionEnd

!macro CloseIE
ieloop:
    FindWindow $0 "IEFrame"
    IntCmp $0 0 iedone
    IsWindow $0 0 iedone
		MessageBox MB_OKCANCEL "$(CLOSE_IE)" IDCancel iequit
    Sleep 1000
    Goto ieloop
iequit:
	Quit
iedone:
!macroend

!macro VerifyUserIsAdmin
UserInfo::GetAccountType
pop $0
${If} $0 != "admin" ;Require admin rights on NT4+
        messageBox mb_iconstop "$(ADMIN_REQUIRED)"
        setErrorLevel 740 ;ERROR_ELEVATION_REQUIRED
        quit
${EndIf}
!macroend

!macro GetCleanDir INPUTDIR
  !define Index_GetCleanDir 'GetCleanDir_Line${__LINE__}'
  Push $R0
  Push $R1
  StrCpy $R0 "${INPUTDIR}"
  StrCmp $R0 "" ${Index_GetCleanDir}-finish
  StrCpy $R1 "$R0" "" -1
  StrCmp "$R1" "\" ${Index_GetCleanDir}-finish
  StrCpy $R0 "$R0\"
${Index_GetCleanDir}-finish:
  Pop $R1
  Exch $R0
  !undef Index_GetCleanDir
!macroend
 
!macro RemoveFilesAndSubDirs DIRECTORY DIRECTORYTOKEEP
  !define Index_RemoveFilesAndSubDirs 'RemoveFilesAndSubDirs_${__LINE__}'
 
  Push $R0
  Push $R1
  Push $R2
 
  !insertmacro GetCleanDir "${DIRECTORY}"
  Pop $R2
  FindFirst $R0 $R1 "$R2*.*"
${Index_RemoveFilesAndSubDirs}-loop:
  StrCmp $R1 "" ${Index_RemoveFilesAndSubDirs}-done
  StrCmp $R1 "${DIRECTORYTOKEEP}" ${Index_RemoveFilesAndSubDirs}-next
  StrCmp $R1 "." ${Index_RemoveFilesAndSubDirs}-next
  StrCmp $R1 ".." ${Index_RemoveFilesAndSubDirs}-next
  
  IfFileExists "$R2$R1\*.*" ${Index_RemoveFilesAndSubDirs}-directory
  ; file
  Delete "$R2$R1"
  goto ${Index_RemoveFilesAndSubDirs}-next
${Index_RemoveFilesAndSubDirs}-directory:
  ; directory
  RMDir /r "$R2$R1"
${Index_RemoveFilesAndSubDirs}-next:
  FindNext $R0 $R1
  Goto ${Index_RemoveFilesAndSubDirs}-loop
${Index_RemoveFilesAndSubDirs}-done:
  FindClose $R0
 
  Pop $R2
  Pop $R1
  Pop $R0
  !undef Index_RemoveFilesAndSubDirs
!macroend

Function RunUninstallerIfNeeded
	StrCpy $DISPLAYING_IDEMIX_LICENSE "${DISPLAY_IDEMIX_LICENSE}"

    ReadRegStr $R0 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "UninstallString"
	StrCmp $R0 "" done

	MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "$(UNINSTALL_CONFIRMATION)" IDOK uninst
	Abort
uninst:
	ClearErrors
	HideWindow
    ExecWait '$R0 /U _?=$INSTDIR'
	StrCpy $DISPLAYING_IDEMIX_LICENSE "No"
done:
FunctionEnd

Function SkipPageIfBeingUpgraded
	StrCmp "Yes" $DISPLAYING_IDEMIX_LICENSE done
    Abort
done:
FunctionEnd

Function .onInit
	setShellVarContext all
	!insertmacro MUI_LANGDLL_DISPLAY
	!insertmacro VerifyUserIsAdmin
FunctionEnd

Section "install"
	# Close Internet Explorer
	!insertmacro CloseIE

	# Check if JRE 1.6 or 1.7 is installed
	Call DetectJRE

	# Extract the embedded ZIP file
#	DetailPrint "$(EXTRACTING_FILES)"
#	SetOutPath "$TEMP\ABC4TrustTemporaryFiles"
#	File "ABC4Trust.zip"

	SetOutPath "$INSTDIR"
	nsisunz::Unzip "$EXEDIR\ABC4Trust.zip" "$INSTDIR"
	Pop $R0
		StrCmp $R0 "success" +3
            MessageBox MB_OK "$(COULD_NOT_EXTRACT) : $EXEDIR\ABC4Trust.zip"
			Goto DeleteZipAndQuit
		Goto ContinueAfterUnzip
DeleteZipAndQuit:
		Var /GLOBAL QUIT_WHEN_CLEANUP_DONE
		StrCpy $QUIT_WHEN_CLEANUP_DONE "1"
ContinueAfterUnzip:
#		MessageBox MB_OK "Zip File : $EXEDIR\ABC4Trust.zip extracted to $INSTDIR : $OUTDIR"
#        Delete "$TEMP\ABC4TrustTemporaryFiles\ABC4Trust.zip"
#		RMDir  "$TEMP\ABC4TrustTemporaryFiles"
#		StrCmp $QUIT_WHEN_CLEANUP_DONE "1" +1 +2
#			Quit
	
	# Extract the icon
	File /oname=abc4trust.ico "${ABC4TRUST_ICON}"

	# Unregister the IE plugin for both 32 and 64 bit architectures
	DetailPrint "$(UNREGISTERING_BROWSER_PLUGINS)"
	ExecDos::exec "$\"$WINDIR\Microsoft.NET\Framework64\v4.0.30319\RegAsm.exe$\" /unregister $\"$INSTDIR\${IE_EXTENSION_DIR}\ABC4TrustActiveX.dll$\""
	ExecDos::exec "$\"$WINDIR\Microsoft.NET\Framework\v4.0.30319\RegAsm.exe$\" /unregister $\"$INSTDIR\${IE_EXTENSION_DIR}\ABC4TrustActiveX.dll$\""
	
	# Register the IE plugin for both 32 and 64 bit architectures
	DetailPrint "$(REGISTERING_BROWSER_PLUGINS)"
	ExecDos::exec "$\"$WINDIR\Microsoft.NET\Framework64\v4.0.30319\RegAsm.exe$\" /codebase /tlb $\"$INSTDIR\${IE_EXTENSION_DIR}\ABC4TrustActiveX.dll$\""
	ExecDos::exec "$\"$WINDIR\Microsoft.NET\Framework\v4.0.30319\RegAsm.exe$\" /codebase /tlb $\"$INSTDIR\${IE_EXTENSION_DIR}\ABC4TrustActiveX.dll$\""

	# Allow ActiveX for Installation Specific Domains
	ExecDos::exec "$\"$WINDIR\RegEdit.exe$\" /s $\"$INSTDIR\${RESOURCES_DIR}\activex_allowed_domains.reg$\""

	# Install IDM root certificates
	ExecDos::exec "$\"$WINDIR\System32\certutil.exe$\" -f -addstore -enterprise root $\"$INSTDIR\${RESOURCES_DIR}\idm_ca.pem$\""
	
	# Register the Firefox plugin
	WriteRegStr HKCU "Software\Mozilla\Firefox\Extensions" "${FIREFOX_EXTENSION_ID}" "$INSTDIR\${FIREFOX_EXTENSION_DIR}"
	
	# Uninstaller - See function un.onInit and section "uninstall" for configuration
	writeUninstaller "$INSTDIR\uninstall.exe"

	# Registry information for add/remove programs
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "DisplayName" "${COMPANYNAME} ${APPNAME}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "QuietUninstallString" "$\"$INSTDIR\uninstall.exe$\" /S"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "InstallLocation" "$\"$INSTDIR$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "DisplayIcon" "$\"$INSTDIR\abc4trust.ico$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "Publisher" "$\"${COMPANYNAME}$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "DisplayVersion" "$\"${VERSIONMAJOR}.${VERSIONMINOR}.${VERSIONBUILD}$\""
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "VersionMajor" ${VERSIONMAJOR}
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "VersionMinor" ${VERSIONMINOR}
	# There is no option for modifying or repairing the install
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "NoModify" 1
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "NoRepair" 1
	# Save the language used for the installer
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "Language" "$LANGUAGE"

	# Start the ABCETray application
	DetailPrint "$(START_APPLICATION)"
	ExecDos::exec /NOUNLOAD /ASYNC "$\"$INSTDIR\ABCETray.exe$\""
SectionEnd

# Uninstaller

Function un.CloseABCE
	push "ABCETray"
	Exch $1
	Push $0
abceloop:
		FindWindow $0 $1
		IntCmp $0 0 abcedone
		SendMessage $0 ${WM_CLOSE} 0 0
		Sleep 100 
		Goto abceloop
abcedone: 
	Pop $0 
	Pop $1
FunctionEnd

Function un.SkipPageIfBeingUpgraded
	${GetOptions} "$CMDLINE" "/U" $R0
	IfErrors +2 0
    Abort
FunctionEnd

Function un.onInit
	SetShellVarContext all
	ReadRegStr $LANGUAGE HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "Language"
	${If} $LANGUAGE == ''
		StrCpy $LANGUAGE "English"
	${EndIf}
	!insertmacro VerifyUserIsAdmin
FunctionEnd

Section "uninstall"
	# Stop the ABCE
#	MessageBox MB_OK "Stop ABCE !"
	DetailPrint "$(STOP_APPLICATION)"
	call un.CloseABCE
	Sleep 5000

	# Close Internet Explorer
	!insertmacro CloseIE

	# Unregister Firefox plugin
	DetailPrint "$(UNREGISTERING_BROWSER_PLUGINS)"
	DeleteRegValue HKCU "Software\Mozilla\Firefox\Extensions" "${FIREFOX_EXTENSION_ID}"

	# Unregister IE plugin
	ExecDos::exec "$\"$WINDIR\Microsoft.NET\Framework64\v4.0.30319\RegAsm.exe$\" /unregister $\"$INSTDIR\${IE_EXTENSION_DIR}\ABC4TrustActiveX.dll$\""
	ExecDos::exec "$\"$WINDIR\Microsoft.NET\Framework\v4.0.30319\RegAsm.exe$\" /unregister $\"$INSTDIR\${IE_EXTENSION_DIR}\ABC4TrustActiveX.dll$\""
	Sleep 1000

#	MessageBox MB_OK "Cleaning up! $INSTDIR : $OUTDIR"
	
	# Remove files
	!insertmacro RemoveFilesAndSubDirs "$INSTDIR\" ""

	# Try to remove the install directory if it's empty
	RMDir "$INSTDIR"
	RMDir "$PROGRAMFILES\${COMPANYNAME}"

	# Remove uninstaller information from the registry
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}"
SectionEnd